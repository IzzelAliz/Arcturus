plugins {
    id 'java'
    id 'maven-publish'
}

group 'io.izzel'
version '1.0.0'

sourceSets {
    processor {}
    test {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testAnnotationProcessor sourceSets.processor.output
}

test {
    useJUnitPlatform()
}

compileProcessorJava {
    options.compilerArgs += [
            'api', 'tree', 'code', 'util', 'comp', 'jvm', 'resources'
    ].collect { "--add-exports=jdk.compiler/com.sun.tools.javac.$it=ALL-UNNAMED".toString() }
}

compileTestJava {
    outputs.upToDateWhen { false }
}

task processorJar(type: Jar) {
    from sourceSets.processor.output
    classifier = 'processor'
}

publishing {
    repositories {
        maven {
            name = "IzzelAliz"
            url = uri("https://maven.izzel.io/releases")
            credentials {
                username = project.findProperty("arcturus.user") ?: System.getenv("ARCTURUS_USERNAME")
                password = project.findProperty("arcturus.key") ?: System.getenv("ARCTURUS_TOKEN")
            }
        }
    }
    publications {
        arcturus(MavenPublication) {
            from(components.java)
            artifact processorJar
        }
    }
}
